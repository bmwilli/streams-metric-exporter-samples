// Simulate flipping a coin tracking max consecutive results for heads and tails
composite HeadsTails {
  graph
    stream <uint64 flip, rstring result > FlipStream = Beacon() {
      param period : 0.01;
      output FlipStream : flip = IterationCount(),
        result = ((random() < 0.5)?"Heads":"Tails");
    }

    stream <FlipStream> FlipStats = Custom(FlipStream) {
      logic
        state : {
          boolean init = InitCustomMetrics();
          mutable rstring lastFlip = "";
          mutable int64 sameCount = 0, maxHeads = 0, maxTails = 0;
        }
        onTuple FlipStream : {
          if (result == lastFlip) {
            sameCount++;
            if ((result == "Heads") && (sameCount > maxHeads)) {
              println("Should set maxHeads = sameCount: " + (rstring)sameCount);
              maxHeads = sameCount;
              setCustomMetricValue("maxConsecutiveHeads",maxHeads);
            } else if ((result == "Tails") && (sameCount > maxTails)) {
              println("Should set maxTails = sameCount: " + (rstring)sameCount);
              maxTails = sameCount;
              setCustomMetricValue("maxConsecutiveTails",maxTails);
            }
          } else {
            sameCount = 0l;
          }
          lastFlip = result;
        }
    }

    (stream <FlipStream> HeadsTuples;
     stream <FlipStream> TailsTuples) as ResultSeparator = Filter(FlipStream) {
      param filter : result == "Heads";
    }

    () as HeadsSink = Custom(HeadsTuples) {
      logic onTuple HeadsTuples : println("Heads!! I Win!!");
    }

    () as TailsSink = Custom(TailsTuples) {
      logic onTuple TailsTuples : println("Tails!! You Lose!!");
    }
}

stateful boolean InitCustomMetrics() {
  createCustomMetric("maxConsecutiveHeads","Max Consecutive Heads", Sys.Counter, 0l);
  createCustomMetric("maxConsecutiveTails","Max Consecutive Tails", Sys.Counter, 0l);
  return true;
} 
